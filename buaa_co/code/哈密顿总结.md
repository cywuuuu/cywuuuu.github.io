### 关于mips汇编challenge 哈密顿回路的经验教训总结

这个题wa了好久，错的满地开花啊啊啊所以写一下，总结一下教训

感谢徐俊豪帅气小哥哥教我算法

感谢帅气助教教我如何分析如何debug

#### 题目回顾和分析

> 输入一个具有 n 个顶点的无向图 G，判断G 是否有哈密尔顿回路。（哈密顿回路问题，建议使用递归解决）
>
> 哈密顿回路的定义： G=(V,E)是一个图，若G中一条路径通过且仅通过每一个顶点一次，称这条路径为哈密顿路径。若G中一个回路通过且仅通过每一个顶点一次，称这个环为哈密顿回路。若一个图存在哈密顿回路，就称为哈密顿图。

1. 典型哈密顿回路

![1632300101831](C:\Users\cyw\AppData\Roaming\Typora\typora-user-images\1632300101831.png)

2. 这个算是哈密顿回路吗？

   ![1632311285994](C:\Users\cyw\AppData\Roaming\Typora\typora-user-images\1632311285994.png)

   ##### 用dfs判断是否含有哈密顿回路的c语言表示

   存图方式可以采用 矩阵，因为对于重边，哈密顿回路要求的是走过的点不能重复，即使有重边，也不影响。

   （其实这个说法里有个小bug， 但是测试数据并没有hack到这个点， 即下面的这个图）

   ![1632311474480](C:\Users\cyw\AppData\Roaming\Typora\typora-user-images\1632311474480.png)

```c
#include <stdio.h>
#include <string.h>
int n, m;
int g[100][100]; 
int vis[20];
int origin = 1;
int end;
int flag;
void dfs(int x) 
{
	vis[x] = 1;	
	int aa = 1;
	for(int i = 1; i <= n; i++)
	{
		aa = vis[i] & aa;
	}
	if(aa && (g[origin][x]))
	{
		flag = 1;
		return;
	}
	for(int i = 1; i <= n; i++)
		if((!vis[i]) && (g[x][i]))
			dfs(i);
	vis[x] = 0;
	return; 
}

int main()
{
	scanf("%d%d", &n, &m);
	for(int i = 0; i < m; i++)
	{
		int a, b;
		scanf("%d%d", &a, &b);
		g[a][b] = 1;
		g[b][a] = 1;
	}
	memset(vis, 0, sizeof(vis));
	dfs(origin);
	printf("%d", flag);	
	return 0;
}
```

#### dfs递归写法与调用规范

##### 下面讨论下调用规范

（有很多高老板ppt上的内容，在L6那一节）

![1632312040815](C:\Users\cyw\AppData\Roaming\Typora\typora-user-images\1632312040815.png)

由于

1. 寄存器数量少，在程序运行时一定存在多次复用寄存器的情况， 

2. 同时因为函数的调用， `原函数` 调用 `子函数` 后一些寄存器将会被覆盖重写，而当 `子函数` 结束return到 `原函数`时，需要复原到 `原函数` 调用 `子函数` 前的状态，以继续 `原函数` 的运行

所以我们需要 保存和恢复 寄存器 

我们用栈这一结构来实现 寄存器的保存和恢复 （也就是 stack push and pop）

![1632312545700](C:\Users\cyw\AppData\Roaming\Typora\typora-user-images\1632312545700.png)

而此时，我们需要保证一些调用规范：

**（我一开始没有严格遵守所以错嗨了呜呜，这点是个大教训， 因为当你遵守调用规范时，至少可以保证本体程序间递归调用，不会出乱子）**

**(以下只是建议遵守， 并且对于给变量挑选合适的寄存器，可以完全看自己的心情)**

![1632312434348](C:\Users\cyw\AppData\Roaming\Typora\typora-user-images\1632312434348.png)

##### 强保护（s寄存器， $ra）

我们处于一个函数中，对于调用的多个子函数都要可能使用的共同变量,相比临时变量，他们有着更长的生命周期， 我们保存在\$s0~\$s7，他们的生命周期 = 函数的生命周期

- **我们对$s寄存器们采取的操作是：**

1. 进入每个函数时， 这个函数用哪些个$s寄存器， 就保存哪个s( push stack )
2. 在函数结束时， 恢复$s到最开始的值，（pop stack） 

这样可以保证，函数的子函数们不会改变这些s寄存器的内容，（即便子函数使用了他们，改变了他们，但最后还是复原了他们），保证了他们的生命周期

==比如本题的 m,n 可以存在内存， 也可以做长周期的$s==

- **对于$ra，我们对他采取的操作是**：

1. 通过jal进入一个函数时， 这个$ra（指向该函数返回时跳回的指令地址呢）得保存好了 （push）
2. 中间可以再次调用自己或其他函数，或者做一些操作， 使得$ra发生变化，但根本不用怕
3. 在函数结束时，要return ，得恢复$s到最开始的值，（pop） 

对于强保护， 我们可以看到一些共同点：**即刚进入函数时保存；退出函数前恢复**

![1632317473440](C:\Users\cyw\AppData\Roaming\Typora\typora-user-images\1632317473440.png)

##### 弱保护（t寄存器， \$v, \$a）

仅仅保存那些生命周期长的函数是不够的， 在函数一次次调用中， 我们很可能还要记录下当前一些临时值的状态， 以便在调用子函数（子函数很可能更改了t）后jr $ra完了，能再恢复他们， 

比如这里的i, 无论是存放在 $t， 还是存放在 \$a中， 都需要对i做保护， 要不然返回后怎么继续我们的循环？

```c
	for(int i = 1; i <= n; i++)
		if((!vis[i]) && (g[x][i]))
			dfs(i);
```

![1632318018530](C:\Users\cyw\AppData\Roaming\Typora\typora-user-images\1632318018530.png)

##### 试着写写dfs

然后就可以把每一段c代码翻译过来啦， 注意 ==出入栈的对应不能有任何差池==

```assembly
### dfs
dfs:
	sw $ra, ($sp)   #save the ra for return purpose 
	subi $sp, $sp, 4
	# push: $sp $t1 (x)
	# push: put it here, sp go down 
	# pop : sp do up,take it out
	move $t1, $a0   #a0 now can be used as a new arg
	#***************************************
	# x as t1, v[x] = 1
	# vis[x] = 1;
	# int aa = 1;
	li $t0, 1 # load t0 as 1
	li $t4, 4
	mult $t4, $t1 
	mflo $t4  #t4 raddr byte
	sw $t0, vis($t4) # save but not change the t0 for aa
	#**********************************
	#for(int i = 1; i <= n; i++)
	#{
	#	aa = vis[i] & aa;
	#}
	li $t2, 1
	lw $t3, n
	for_t2_begin:
		sle $t4, $t2, $t3
		beq $t4, $0, for_t2_end
		
		li $t7, 4
		mult $t7, $t2
		mflo $t7
		#t7 addr = t2 * 4
		lw $t5, vis($t7) #get vis[t7], vis[i]
		and $t0, $t5, $t0
		addi $t2, $t2, 1
		j for_t2_begin
	for_t2_end:
	# aa as t0
	# i  as t2 (fade)
	# n  as t3 
	# x  as t1
	#*****************************
	#if(aa && (g[origin][x]))
	#{
	#	flag = 1;
	#}
	#get g[origin][x]
	lw $t4, origin
	li $t7, 4
	mult $t3, $t4 # origin*n
	mflo $t5
	add $t5, $t5, $t1 
	mult $t7, $t5
	mflo $t5
	#t5 = 4(ori *n + x)
	lw $t5, array($t5)
	#t5 g[ori][x]
	and $t2, $t0, $t5
	beq $t2, $0, pass1
	li $s3, 1
	addi $sp, $sp, 4
	lw $ra, ($sp) ## 注意这里 ！！！别丢下任何一个return（jr $ra）
	## 这里强保护ra的弹栈
	jr $ra	
	pass1:
	# aa as t0
	# condition as t2 
	# n  as t3 
	# x  as t1
	# origin as t4
	# raddr as t5 (pop)
	# g[ori][x] as t5
	# flag as s3
	#*********************************
	#for(int i = 1; i <= n; i++)
	#	if((!vis[i]) && (g[x][i]))
	#		dfs(i);	
	li $t9, 1
	for_s0_begin:
	sle $t2, $t9, $t3
	beq $t2, $0, for_s0_end 
		li $t4, 4
		mult $t4, $t9
		mflo $t4
		lw $t6, vis($t4)
		beq $0, $t6, eq_zero
		beq $t6, 1, eq_one
		eq_zero:
		li $t6, 1
		j end_if
		eq_one:
		li $t6, 0
		j end_if
		end_if:
		#t6 !vis[i]
		mult $t3, $t1 
		mflo $t5
		add $t5, $t5, $t9
		li $t4, 4
		mult $t4, $t5
		mflo $t4
		lw $t7, array($t4)
		#t7 g[x][i]
		and $t4, $t6, $t7 

		#get condi $t4, if(t4) go dfs
		beq $t4, $0, pass2
		# sw $ra, ($sp)   #sp points to the next
		# subi $sp, $sp, 4

		sw $t1, ($sp)   #save the x as t1 这里是弱保护t1 即 所在点x
		subi $sp, $sp, 4
		sw $t9, ($sp)   #save the i as t9 这里是弱保护t9 即 可能临点i
		subi $sp, $sp, 4
		move $a0, $t9
		jal dfs 
		addi $sp, $sp, 4
		lw $t9, ($sp) 
		addi $sp, $sp, 4
		lw $t1, ($sp)
		
		pass2:

	addi $t9, $t9, 1
	j for_s0_begin
	for_s0_end:
	
	# i as t0
	# branch as t2 
	# n  as t3 
	# x  as t1
	# 1 as t4 -> cond as t4
	# raddr as t5
	# flag as s3
	# !vis[i] as t6
	# g[x][i] as t7
	#*********************
	# vis[x] = 0;	
	li $t4, 4
	mult $t1, $t4
	mflo $t4
	sw $0, vis($t4)
	#**********************
	# return;
	addi $sp, $sp, 4
	lw $ra, ($sp) 
		## 这里强保护ra的弹栈！！
	jr $ra	
	
# s3 reserved for flag 
```

